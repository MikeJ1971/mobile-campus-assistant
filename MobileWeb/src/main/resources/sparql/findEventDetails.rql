PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ical:      <http://www.w3.org/2002/12/cal/ical#>
PREFIX mca:     <http://org.ilrt.mca/registry#>

CONSTRUCT {
    $x ical:dtstart $startdate .
    $x ical:dtend $enddate .
    $x ical:description $description .
    $x ical:summary $summary .
    $x ical:location $location .
    $x ical:organizer $orgname .
    $x ical:calAddress $orgemail .
    $x ical:uid $id .
    $x mca:template <template://eventDetails.ftl> .
    $x rdf:type ical:Vevent .
 } WHERE {
    GRAPH ?graph {
        $x ical:uid $id .
        $x ical:dtstart $y .
        {
            {  $y ical:dateTime $startdate  } UNION { $y ical:date $startdate }
        }
        OPTIONAL { $x ical:description $description . }
        $x ical:summary $summary .
        $x rdf:type ical:Vevent .
        OPTIONAL
         {
            $x ical:dtend $z .
            {  $z ical:dateTime $enddate  } UNION { $z ical:date $enddate }
         }
        OPTIONAL { $x ical:location $location . }
        OPTIONAL { $x ical:organizer [ ical:cn $orgname ] . }
        OPTIONAL { $x ical:calAddress $orgemail . }
    }
}
