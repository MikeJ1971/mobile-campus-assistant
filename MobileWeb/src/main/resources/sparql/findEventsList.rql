PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ical:      <http://www.w3.org/2002/12/cal/ical#>
PREFIX mca:     <http://org.ilrt.mca/registry#>
PREFIX xsd:      <http://www.w3.org/2001/XMLSchema#>
PREFIX afn:     <http://jena.hpl.hp.com/ARQ/function#>
PREFIX fn:      <http://www.w3.org/2005/xpath-functions#>

CONSTRUCT {
    $x ical:dtstart $sdate .
    $x ical:dtend $edate .
    $x ical:summary $summary .
    $x ical:uid $id .
    $x mca:template <template://eventList.ftl> .
    $x rdf:type ical:Vevent .
} WHERE {
    GRAPH ?graph
    {
        $x rdf:type ical:Vevent .
        $x ical:uid $id .
        $x ical:summary $summary .
        $x ical:dtstart $y .
        $y ical:dateTime $sdate .
        OPTIONAL {
            $x ical:dtend $z .
            $z ical:dateTime $edate .
        }
        FILTER (xsd:dateTime($sdate) >= xsd:dateTime(?startDate) &&
            xsd:dateTime($sdate) <= xsd:dateTime(?endDate))
    }
}
